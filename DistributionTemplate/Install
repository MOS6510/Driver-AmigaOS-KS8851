; Installer script for Amiga1200 Plus Network Device Driver ("ksz8851").
; Copyright (c) 2020 Heiko Pruessing.
; $VER: "Install ksz8851 network device driver" 1.0 (03.10.2020)

; ----------------------------- CONSTS ----------------------------------------

(set #current_version "1.0")

(set #dev-name "ksz8851.device")
(set @default-dest "RAM:")
(set #config_file_env       "env:sana2/ksz8851.config")
(set #config_file_envarc "envarc:sana2/ksz8851.config")
(set #src_dev_name        "network/ksz8851.device.020")

(set #msg-os-too-low    "AmigaOS 2.04 or greater is required.")
(set #msg-help-copy-lib "Copy the SANA2 Network Device Driver to the system.")

(set #msg-final-installed
   (cat "Installation complete!\n\n"
      "ksz8851 device driver 1.0 is on your system!\n\nMay the force be with you! :-)\n"
   )
)

(set #wrongversion
(cat "You have an old version of 'Installer' installed."
     "\n\nThe installation procedure needs at least Amiga Installer 42.9.\n\n"
     "Please check your configuration and try again!"
))

(set #intro (cat "\nAmiga1200 Plus Network Device Driver " #current_version 
   "\n====================================\n(c) 2019 Heiko Pruessing\n" 
   "Welcome to the installation program of Amiga1200 Plus network device driver ('ksz8851.device'). "
   "Use this tool to install the device driver on your Amiga system or to update a previously installed release.")
)   


; ----------------------------- Start -----------------------------------------

(message #intro)

; We can't handle this script without Installer 42.9 or better!
(if (< @installer-version 2752521)
  (
    (message #wrongversion)
    (exit (quiet))
  )
)

; Check OS version isn't too old
(if (< (/ (getversion) 65536) 37)
   (abort #msg-os-too-low)
)

(complete 0)
      
; ###### Install Files ########      

;
; Install Device
;
(copylib 
	(prompt "Installing the network device driver.")
	(help @copylib-help)
   (source #src_dev_name)
   (dest "DEVS:networks")
   (newname "ksz8851.device")
   (confirm)
)

(complete 30)

; Copy new default configuration file(s) only if none exists
(if ( = (exists #config_file_env) 0 ) 
   (
		(copyfiles
		   (source "ksz8851.config")
		   (dest "env:sana2")
		   (optional "force" "askuser")
		   (confirm)
		)
   ) 
)

(complete 60)

(if ( = (exists #config_file_envarc) 0 ) 
   (
      (copyfiles
         (source "ksz8851.config")
         (dest "envarc:sana2")
         (confirm)
         (files)
      )
   ) 
)

(complete 90)

; Install Roadshow configuration if present
(if ( = (exists "Devs:NetInterfaces" (noreq) ) 2 )
   (
		(copyfiles
		   (source "Roadshow/")
		   (dest "DEVS:NetInterfaces")
		   (optional "force" "askuser")
		   (pattern "#?")
		   (confirm)
		   (files)
		   (infos)
		   (prompt "Copying a configuration for TCP Stack Roadshow")
		   (help "Copying a configuration for TCP Stack Roadshow")
		)
   ) 
)

;
; Finish Installation
;
(complete 100)
(exit (cat #msg-final-installed) (quiet))


